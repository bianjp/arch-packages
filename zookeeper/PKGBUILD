pkgname=bianjp-zookeeper
pkgver=3.6.3
pkgrel=1
pkgdesc='Open-source server which enables highly reliable distributed coordination'
groups=("bianjp")
arch=('any')
url='https://zookeeper.apache.org/'
license=('Apache')
depends=('java-runtime-headless>=8')

_mirror_url='https://mirrors.tuna.tsinghua.edu.cn/apache'
_backup_mirror_url="https://www-us.apache.org/dist"
source=(
  "${_mirror_url}/zookeeper/zookeeper-${pkgver}/apache-zookeeper-${pkgver}-bin.tar.gz"
  "${_backup_mirror_url}/zookeeper/zookeeper-${pkgver}/apache-zookeeper-${pkgver}-bin.tar.gz.sha512"
  "zookeeper.service"
)
md5sums=('SKIP' 'SKIP' 'SKIP')

package() {
  sha512sum -c "${srcdir}/apache-zookeeper-${pkgver}-bin.tar.gz.sha512" > /dev/null

  install -dm755 "${pkgdir}/opt"
  install -dm755 "${pkgdir}/usr/bin"
  install -dm755 "${pkgdir}/var/lib/zookeeper"
  cp -r "${srcdir}/apache-zookeeper-${pkgver}-bin" "${pkgdir}/opt/zookeeper"
  mv "${pkgdir}/opt/zookeeper/conf/zoo_sample.cfg" "${pkgdir}/opt/zookeeper/conf/zoo.cfg"
  echo -e '\nadmin.enableServer=false\n4lw.commands.whitelist=*' >> "${pkgdir}/opt/zookeeper/conf/zoo.cfg"
  sed -i 's#dataDir=.*#dataDir=/var/lib/zookeeper#' "${pkgdir}/opt/zookeeper/conf/zoo.cfg"

  rm -r "${pkgdir}"/opt/zookeeper/bin/*.cmd
  rm -r "${pkgdir}"/opt/zookeeper/bin/*.txt
  rm -r "${pkgdir}"/opt/zookeeper/docs

  local filename
  local executable
  for executable in "${pkgdir}"/opt/zookeeper/bin/*.sh; do
    filename="$(basename "$executable" .sh)"
    ln -s "../../opt/zookeeper/bin/${filename}.sh" "${pkgdir}/usr/bin/${filename}"
    sed -i 's#ZOOBINDIR=.*#ZOOBINDIR=/opt/zookeeper/bin#' "${pkgdir}/usr/bin/${filename}"
  done

  install -Dm644 "${srcdir}/zookeeper.service" "${pkgdir}/usr/lib/systemd/system/zookeeper.service"
}
